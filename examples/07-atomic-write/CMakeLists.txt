#
# SPDX-License-Identifier: BSD-3-Clause
# Copyright 2020-2022, Intel Corporation
# Copyright 2021, Fujitsu
#

cmake_minimum_required(VERSION 3.3)
project(atomic-write C)

set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH}
	${CMAKE_SOURCE_DIR}/../cmake
	${CMAKE_SOURCE_DIR}/../../cmake)

include(${CMAKE_SOURCE_DIR}/../../cmake/functions.cmake)

# check if all required IBV_ADVISE_MR* flags are supported
are_ibv_advise_flags_supported(IBV_ADVISE_MR_FLAGS_SUPPORTED)
if(IBV_ADVISE_MR_FLAGS_SUPPORTED)
	message(STATUS "All required IBV_ADVISE_MR* flags are supported")
else()
	if(SKIP_IF_IBV_ADVISE_MR_FLAGS_NOT_SUPPORTED)
		message(WARNING "Required IBV_ADVISE_MR_ADVICE_PREFETCH_WRITE and IBV_ADVISE_MR_FLAG_FLUSH flags are NOT supported. Skipping ...")
		return()
	else()
		message(FATAL_ERROR "Required IBV_ADVISE_MR_ADVICE_PREFETCH_WRITE and IBV_ADVISE_MR_FLAG_FLUSH flags are NOT supported.")
	endif()
endif()

# set LIBRT_LIBRARIES if linking with librt is required
check_if_librt_is_required()

set(LIBPMEM_REQUIRED_VERSION 1.6)

set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -std=gnu99")

find_package(PkgConfig QUIET)

if(PKG_CONFIG_FOUND)
	pkg_check_modules(LIBRPMA librpma)
	pkg_check_modules(LIBIBVERBS libibverbs)
	pkg_check_modules(LIBPMEM QUIET libpmem>=${LIBPMEM_REQUIRED_VERSION})
endif()
if(NOT LIBRPMA_FOUND)
	find_package(LIBRPMA REQUIRED librpma)
endif()
if(NOT LIBIBVERBS_FOUND)
	find_package(LIBIBVERBS REQUIRED libibverbs)
endif()
if(NOT LIBPMEM_FOUND)
	find_package(LIBPMEM ${LIBPMEM_REQUIRED_VERSION} QUIET)
endif()

link_directories(${LIBRPMA_LIBRARY_DIRS})

function(add_example name)
	set(srcs ${ARGN})
	add_executable(${name} ${srcs})
	target_include_directories(${name}
		PRIVATE
			${LIBRPMA_INCLUDE_DIRS}
			../common)
	target_link_libraries(${name} rpma ${LIBIBVERBS_LIBRARIES} ${LIBRT_LIBRARIES})

	if(LIBPMEM_FOUND)
		target_include_directories(${name}
			PRIVATE ${LIBPMEM_INCLUDE_DIRS})
		target_link_libraries(${name} ${LIBPMEM_LIBRARIES})
		target_compile_definitions(${name}
			PRIVATE USE_LIBPMEM)
	endif()
endfunction()

add_example(server server.c ../common/common-conn.c)
add_example(client client.c ../common/common-conn.c)
